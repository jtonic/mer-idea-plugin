import org.jetbrains.intellij.tasks.RunIdeTask

buildscript {

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven {
            url 'http://dl.bintray.com/jetbrains/intellij-plugin-service'
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-noarg:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
        classpath "gradle.plugin.org.jetbrains.intellij.plugins:gradle-intellij-plugin:$intellijPluginVersion"
        classpath 'de.undercouch:gradle-download-task:3.4.3'
    }
}

group = 'ro.jtonic.plugins'
version = "0.0.1-SNAPSHOT"

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'kotlin'
apply plugin: 'kotlin-allopen'
apply plugin: 'kotlin-noarg'
apply plugin: "org.jetbrains.intellij"
apply plugin: 'de.undercouch.download'

dependencies {
    compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion") {
        force = true
    }
    compile("org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion") {
        force = true
    }
    compile("io.kotlintest:kotlintest:$kotlinTestVersion") {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-reflect'
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
    }
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:$kotlinCoroutinesVersion"

    testCompile "org.testng:testng:$testNgVersion"
}

compileJava {
    sourceCompatibility = 1.8
}

[compileKotlin, compileTestKotlin]*.configure {
    kotlinOptions {
        suppressWarnings = true
        languageVersion = kotlinLanguageVersion
        apiVersion = kotlinApiVersion
        jvmTarget = kotlinJvmTarget
        javaParameters = true
        incremental = true
        freeCompilerArgs = ["-Xjsr305=strict", "-Xjvm-default=enable"]
    }
}

// Use the  gradle-download-task plugin to download:
// 1. IntelliJ Idea CE from
// https://www.jetbrains.com/intellij-repository/releases/com/jetbrains/intellij/idea/ideaIC/173.3727.127/
// ideaIC-173.3727.127.pom
// ideaIC-173.3727.127-sources.jar
// ideaIC-173.3727.127.zip

// 2. all OS flavoured jbre version jbrex8u152b1136.29 from
// https://dl.bintray.com/jetbrains/intellij-jdk/

// gradle-download-task:
//      https://github.com/michel-kraemer/gradle-download-task
//      https://michelkraemer.com/recipes-for-gradle-download/

task downloadIdeaSandbox(type: Download) {
    group 'mer'
    src "https://www.jetbrains.com/intellij-repository/releases/com/jetbrains/intellij/idea/ideaIC/${ideaICVersion}/ideaIC-${ideaICVersion}.zip"
    dest "$projectDir/lib/ideaID/ideaIC-${ideaICVersion}.zip"
    overwrite false
}

//noinspection GroovyAssignabilityCheck
task uzipIdeaSandbox(type: Copy, dependsOn: downloadIdeaSandbox) {
    group 'mer'
    from zipTree(downloadIdeaSandbox.dest)
    into "$projectDir/lib/ideaID/ideaIC-${ideaICVersion}"
}

task downloadJbrex(type: Download) {
    group 'mer'
    src([
            "https://dl.bintray.com/jetbrains/intellij-jdk/${jbrexVersion}_osx_x64.tar.gz",
            "https://dl.bintray.com/jetbrains/intellij-jdk/${jbrexVersion}_linux_x64.tar.gz",
            "https://dl.bintray.com/jetbrains/intellij-jdk/${jbrexVersion}_linux_x86.tar.gz",
            "https://dl.bintray.com/jetbrains/intellij-jdk/${jbrexVersion}_windows_x64.tar.gz",
            "https://dl.bintray.com/jetbrains/intellij-jdk/${jbrexVersion}_windows_x86.tar.gz"
    ])
    dest "$projectDir/lib/jbre/${jbrexVersion}"
    overwrite false
}

// tasks dependencies
// https://stackoverflow.com/questions/32907275/gradle-custom-task-which-runs-multiple-tasks
//noinspection GroovyAssignabilityCheck
task prepareIde {
    group 'mer'
    dependsOn 'downloadIdeaSandbox'
    dependsOn 'uzipIdeaSandbox'
    dependsOn 'downloadJbrex'
    tasks.findByName('uzipIdeaSandbox').mustRunAfter 'downloadIdeaSandbox'
    tasks.findByName('downloadJbrex').mustRunAfter 'uzipIdeaSandbox'
}


intellij {
    // for IntelliJ Idea build numbers see
    // https://www.jetbrains.org/intellij/sdk/docs/basics/getting_started/build_number_ranges.html

    // uncomment version and comment out the localPath to fetch the IDE from jetbrains REPO.
    // Jetbrains product releases -> https://www.jetbrains.com/intellij-repository/releases
    // 181.4203.550 -> 2018.1
    version = 'IC-173.3727.127'
    updateSinceUntilBuild = false
    // The kotlin idea plugin versions -> https://plugins.jetbrains.com/plugin/6954-kotlin
    plugins 'org.jetbrains.kotlin:1.2.51-release-IJ2017.3-1', 'maven', 'gradle'
    pluginName 'Mer'
    // MAC-OS
    // localPath 'lib/ideaIC-173.3727.127'
    // Windows
    // version 'IC-181.4445.78'
    // localPath '/Users/antonelpazargic/Library/Application Support/JetBrains/Toolbox/apps/IDEA-C/ch-0/173.4548.28'
}


// https://github.com/JetBrains/gradle-intellij-plugin/blob/master/src/main/groovy/org/jetbrains/intellij/tasks/RunIdeTask.groovy
//noinspection GroovyAssignabilityCheck
task runIdeWithDifferentJvm(type: RunIdeTask) {
    jbreVersion jbrexVersion
}

runIde {
    // where jbre can be downloaded from
    // https://dl.bintray.com/jetbrains/intellij-jdk/
    // https://dl.bintray.com/jetbrains/intellij-jdk/jbrex8u152b1136.29_osx_x64.tar.gz
    // jbreVersion 'jbrex8u152b1136.29'
    // ideaDirectory '/Users/antonelpazargic/Library/Application Support/JetBrains/Toolbox/apps/IDEA-C/ch-0/173.4548.28'
}

patchPluginXml {
    changeNotes """
      Add change notes here.<br>
      <em>most HTML tags may be used</em>"""
}

task wrapper(type: Wrapper) {
    gradleVersion = project.gradleVersion
    distributionType = Wrapper.DistributionType.ALL
    //    distributionUrl = "gradle-$gradleVersion-all.zip"
    distributionUrl = "http://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

idea {
    project {
        jdkName = javaVersion
        languageLevel = javaVersion
    }
}
